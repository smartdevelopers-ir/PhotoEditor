plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {

    defaultConfig {
        minSdk 19
        targetSdk 36
        compileSdkVersion 36
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'ir.smartdevelopers.smartphotoeditor'
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.androidxcore
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.exifinterface
    implementation libs.glide

    annotationProcessor libs.compiler

}
group = 'ir.smartdevelopers'
version = '1.1.4'

afterEvaluate{
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'ir.smartdevelopers'
                artifactId = 'photo-editor'
                pom {
                    name = 'photo-editor'
                    description = 'photo editor library'
                    url = 'https://github.com/smartdevelopers-ir/PhotoEditor'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'smartdevelopers'
                            name = 'mostafa babaei'
                            email = 'info@smartdevelopers.ir'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/smartdevelopers-ir/PhotoEditor.git'
                        developerConnection = 'scm:git:ssh://github.com/smartdevelopers-ir/PhotoEditor.git'
                        url = 'http://github.com/smartdevelopers-ir/PhotoEditor'
                    }

                }
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }

}
signing {
    sign configurations.archives
}
